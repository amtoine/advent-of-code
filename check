#!/usr/bin/env nu


def get-aoc-header [
  login: string
] {
  let aoc_login = (
    gpg --quiet --decrypt ($login | path expand)
    | from toml
  )
  let header = [
    Cookie $'session=($aoc_login.cookie)'
    User-Agent $'email: ($aoc_login.mail)'
  ]

  $header
}


def fetch-aoc-answers [
  day: int
  login: string
] {
  let url = $'https://adventofcode.com/2022/day/($day)'

  let result = (fetch -H (get-aoc-header $login) $url)
  let answers = (
    $result
    | lines
    | parse "<p>Your puzzle answer was <code>{answer}</code>{rest}"
  )

  if ($answers | is-empty) {
    print "Puzzle not answered"
  } else {
    {
      silver: $answers.answer.0
      gold: $answers.answer.1
    }
  }
}


def main [
  scripts: string
  inputs: string
  login: string
  ext: string = "txt"
] {
  ls $scripts
  | each {|script id|
    print $"(ansi yellow_bold)Day ($id + 1)(ansi reset):"

    print -n $"  (ansi default_dimmed)Pulling true answers...(ansi reset) "
    let answers = (fetch-aoc-answers ($id + 1) $login)
    print "ok"

    print -n $"  (ansi default_dimmed)Running silver...(ansi reset) "
    let silver = (^$script.name $"($inputs)/($id + 1).($ext)")
    print "ok"
    print -n $"  (ansi default_dimmed)Running gold...(ansi reset) "
    let gold = (^$script.name $"($inputs)/($id + 1).($ext)" --gold)
    print "ok"

    mut good = true
    if ($silver != $answers.silver) {
      print -n $"(ansi red_bold)silver bad(ansi reset): (ansi red)expected ($answers.silver), got ($silver)(ansi reset)"
      $good = false
      print ""
    }
    if ($gold != $answers.gold) {
      print -n $"(ansi red_bold)gold bad(ansi reset): (ansi red)expected ($answers.gold), got ($gold)(ansi reset)"
      $good = false
      print ""
    }

    if ($good) {
      print $"(ansi green_bold)good(ansi reset)"
    }
  }
}